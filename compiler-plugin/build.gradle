apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    // WARNING: don't change the configuration name because it's used by shadowJar
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compileOnly "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlin_version"
    compileOnly "com.intellij:openapi:7.0.3"

    testImplementation "io.kotlintest:kotlintest-runner-junit4:$kotlin_test_version"
    testImplementation project(":testing-plugin")
    testRuntimeOnly project(path: ':compiler-plugin', configuration: 'shadow')

    testRuntimeOnly "io.arrow-kt:arrow-annotations:$VERSION_NAME"
    testRuntimeOnly "io.arrow-kt:arrow-core-data:$VERSION_NAME"
    testRuntimeOnly "io.arrow-kt:arrow-optics:$VERSION_NAME"
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
        jvmTarget = "1.8"
    }
}
jar {
    manifest {
        attributes["Specification-Title"] = project.name
        attributes["Specification-Version"] = project.version
        attributes["Implementation-Title"] = "arrow.meta.plugin.compiler"
        attributes["Implementation-Version"] = project.version
    }
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

shadowJar {
    configurations = [project.configurations.compile]
    relocate 'org.jetbrains.kotlin.com.intellij', 'com.intellij'
    relocate 'org.jetbrains.kotlin.load', 'kotlin.reflect.jvm.internal.impl.load'
}

// TODO: Temporary configuration
// A temporary version from a fork of
// com.github.tschuchortdev:kotlin-compile-testing
// is needed until it's published with the contribution
// Remove when having only releases from Maven Central
repositories {
    maven { url "https://jitpack.io" }
}

test {
  testLogging.showStandardStreams = true
  systemProperty "CURRENT_VERSION", "$VERSION_NAME"
}

apply from: 'https://raw.githubusercontent.com/arrow-kt/arrow/master/gradle/gradle-mvn-push.gradle'
