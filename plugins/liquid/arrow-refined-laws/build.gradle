plugins {
    id "org.jetbrains.kotlin.multiplatform"
    id "org.jlleitschuh.gradle.ktlint"
}

apply from: "${rootDir}/${SUBPROJECT_MPP}"

configurations.create("toCopy")

kotlin {
    targets.all {
        compilations.all {
            kotlinOptions {
                println("${rootDir}/plugins/liquid/refined-types-plugin/build/libs/arrow-refined-types-plugin-1.5.10-SNAPSHOT.jar")
                freeCompilerArgs += ["-Xplugin=${rootDir}/plugins/liquid/refined-types-plugin/build/libs/arrow-refined-types-plugin-1.5.10-SNAPSHOT.jar",
                                     "-P", "plugin:arrow.meta.plugin.compiler:generatedSrcOutputDir=${buildDir}"]
            }
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                api project(":arrow-refined-types")
            }
        }
        jvmMain {
            dependencies {
                api("io.arrow-kt:arrow-core:$ARROW_VERSION") {
                    exclude group: "org.jetbrains.kotlin"
                }
            }
        }
    }
}

dependencies {
    "toCopy"(project(":refined-types-plugin"))
    kotlinCompilerClasspath(fileTree('build/libs') { include '*.jar' })
}

// Script
tasks.register("setup") {
    doLast {
        if (!file("build/libs").exists()) {
            file("build/libs").mkdirs()
        }
        copy {
            from(configurations.getAt("toCopy")).into("build/libs")
        }
    }
}


compileKotlinJvm.dependsOn(":refined-types-plugin:jar")
compileKotlinJvm.dependsOn("setup")