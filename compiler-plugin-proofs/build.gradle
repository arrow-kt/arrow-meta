apply plugin: 'kotlin'

dependencies {
    compileOnly project(':compiler-plugin-core')

    testImplementation "org.junit.jupiter:junit-jupiter:$JUNIT_VERSION"
    testImplementation project(":meta-test")
    testRuntimeOnly project(':compiler-plugin-core')
    testRuntimeOnly project(':compiler-plugin-proofs')
    testRuntimeOnly("io.arrow-kt:arrow-core-data:$ARROW_VERSION") {
        exclude group: "org.jetbrains.kotlin"
    }
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-XXLanguage:+InlineClasses", "-Xjvm-default=enable"]
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
        jvmTarget = "1.8"
    }
}

jar {
    manifest {
        attributes["Specification-Title"] = project.name
        attributes["Specification-Version"] = project.version
        attributes["Implementation-Title"] = "arrow.meta.plugin.compiler.proofs"
        attributes["Implementation-Version"] = project.version
    }
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    systemProperty "CURRENT_VERSION", "$VERSION_NAME"
    systemProperty "ARROW_VERSION", "$ARROW_VERSION"
    jvmArgs '-Dkotlin.compiler.execution.strategy="in-process"'
}
