plugins {
    id "org.jetbrains.intellij" version "$INTELLIJ_GRADLE_PLUGIN_VERSION"
}

apply plugin: 'ank-gradle-plugin'
apply plugin: 'kotlin-kapt'

dependencies {
    compile project(':idea-plugin')
    compile "junit:junit:4.13"
}

intellij {
    version = "$INTELLIJ_IDEA_VERSION"
    plugins = ["org.jetbrains.kotlin:${KOTLIN_IDEA_VERSION}"]
}

task printcp {
    doLast {
        println sourceSets.main.compileClasspath.each { println it }
        println sourceSets.main.runtimeClasspath.each { println it }
    }
}

ext {
    ext.set("LOCAL_PATHS_FILE", "docs/localPaths.log")
    if ( !new File("$LOCAL_PATHS_FILE").exists() )  new File("$LOCAL_PATHS_FILE").createNewFile()
}

ank {
    source = file("${projectDir}/docs")
    target = file("${projectDir}/build/site")
    classpath = sourceSets.main.runtimeClasspath + layout.files(new File("$LOCAL_PATHS_FILE").readLines())
}

task getLocalPaths {
    doLast {
        def libraries = ['kotlin-plugin.jar', 'platform-api.jar', 'platform-impl.jar', 'idea.jar']
        def localPaths = libraries.collect{ library -> sourceSets.main.compileClasspath.find{ path -> path.name == "$library" } }
        def localPathsFile = new File("$LOCAL_PATHS_FILE")
        localPathsFile.write localPaths.join("\n")
        layout.files(new File("$LOCAL_PATHS_FILE").readLines()).each { println it.path }
    }
}

task validateDoc {
    dependsOn 'runAnk'
}

task buildMetaDoc {
    group = "Arrow Meta"
    description = "Run validation"
    dependsOn 'getLocalPaths'
    dependsOn 'validateDoc'
}

validateDoc.mustRunAfter getLocalPaths

compileKotlin.kotlinOptions.freeCompilerArgs += ["-Xskip-runtime-version-check"]
