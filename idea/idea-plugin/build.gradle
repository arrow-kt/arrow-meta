plugins {
  id 'org.jetbrains.intellij' version '0.4.10'
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.intellij'

dependencies {
  // provided by the kotlin-plugin
  // runtime "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  compile project(path: ':compiler-plugin', configuration: 'shadow')

  implementation("org.celtric.kotlin:kotlin-html:0.1.4") {
    // already provided by the kotlin-plugin
    exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib"
    exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk8"
  }

  implementation(project(":idea:compile")) {
    // already provided by the kotlin-plugin
    exclude group: "org.jetbrains.kotlin", module: "kotlin-compiler-embeddable"
    exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib"
    exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk8"
    exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk7"
    exclude group: "org.jetbrains.kotlin", module: "kotlin-annotation-processing-embeddable"
  }

  testImplementation(project(":idea:compile")) {
    // already provided by the kotlin-plugin
    exclude group: "org.jetbrains.kotlin", module: "kotlin-compiler-embeddable"
    exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib"
    exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk8"
    exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk7"
    exclude group: "org.jetbrains.kotlin", module: "kotlin-annotation-processing-embeddable"
  }
  testRuntimeOnly("io.arrow-kt:arrow-annotations:$VERSION_NAME") {
    // already provided by the kotlin-plugin
    exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib"
  }
  testRuntimeOnly("io.arrow-kt:arrow-core-data:$VERSION_NAME"){
    // already provided by the kotlin-plugin
    exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk7"
  }
}

compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
jar {
  manifest {
    attributes["Specification-Title"] = project.name
    attributes["Specification-Version"] = project.version
    attributes["Implementation-Title"] = "arrow.meta.plugin.idea"
    attributes["Implementation-Version"] = project.version
  }
}

buildSearchableOptions.enabled = false

intellij {
  version = "2019.2.1"
  pluginName = "Arrow plugin"
  setPlugins("gradle", "java", "org.jetbrains.kotlin:$kotlin_version-release-IJ2019.2-1")
}

patchPluginXml {
  changeNotes """
      Add change notes here.<br>
      <em>most HTML tags may be used</em>"""
}

runIde {
  jvmArgs '-Xmx2G'
}

test {
  testLogging.showStandardStreams = true
  systemProperty "CURRENT_VERSION", "$VERSION_NAME"
}

apply from: 'https://raw.githubusercontent.com/arrow-kt/arrow/master/gradle/gradle-mvn-push.gradle'
