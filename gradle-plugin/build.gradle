plugins {
  id 'java-gradle-plugin'
  id 'maven-publish'
  id 'com.gradle.plugin-publish' version "0.10.1"
}

apply plugin: 'kotlin'

group = 'io.arrow-kt'
version = '0.10.3-rc-2'
def pluginId = 'io.arrow-kt.arrow'

processResources {
  filesMatching('**/plugin.properties') {
    filter {
      it.replace('%COMPILER_PLUGIN_VERSION%', VERSION_NAME)
    }
  }
}

dependencies {
  implementation gradleApi()
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation "org.jetbrains.kotlin:kotlin-gradle-plugin-api:$kotlin_version"
  implementation "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  implementation "io.github.classgraph:classgraph:$class_graph_version"
  runtimeOnly "io.arrow-kt:arrow-meta-compiler-plugin:$VERSION_NAME"
}

compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

jar {
  manifest {
    attributes["Specification-Title"] = project.name
    attributes["Specification-Version"] = project.version
    attributes["Implementation-Title"] = pluginId
    attributes["Implementation-Version"] = project.version
  }
}

// Required to put the Kotlin plugin on the classpath for the functional test suite
tasks.withType(PluginUnderTestMetadata).configureEach {
  pluginClasspath.from(configurations.compileOnly)
}

gradlePlugin {
  plugins {
    arrow {
      id = pluginId
      implementationClass = "arrow.meta.plugin.gradle.ArrowGradlePlugin"
    }
  }
}

pluginBundle {
  website = "http://arrow-kt.io/"
  vcsUrl = "https://github.com/arrow-kt/arrow-meta"
  description = "The Arrow compiler plugin."
  tags = ["kotlin", "compiler", "arrow", "plugin", "meta"]

  plugins {
    arrow {
      displayName = "Arrow compiler plugin"
    }
  }
}
