buildscript {
  ext.set("PATH_APIDOCS", "${rootDir}/docs/docs/apidocs")
}

plugins {
  id 'org.jetbrains.kotlin.jvm' version "$KOTLIN_VERSION" apply false
  id "org.jetbrains.dokka" version "$DOKKA_VERSION" apply false
}

// APIDOC
configure(subprojects - project("docs")
) {
  apply plugin: "org.jetbrains.dokka"
  dokka {
    outputFormat = 'jekyll'
    outputDirectory = PATH_APIDOCS
    configuration {
        includes = ['README.md']
    }
  }
}

// Publication
configure(subprojects - project("docs")
) {
  group = GROUP
  version = VERSION_NAME
}

// JVM Publication
configure(subprojects -
        project("docs") -
        project("gradle-plugin-commons") -
        project("arrow-refined-types") // MPP subprojects
) {
  afterEvaluate {
    jar {
      archiveBaseName = POM_ARTIFACT_ID
      manifest {
        attributes["Specification-Title"] = project.name
        attributes["Specification-Version"] = project.version
        attributes["Implementation-Title"] = POM_NAME
        attributes["Implementation-Version"] = project.version
      }
    }
  }
  apply from: "https://raw.githubusercontent.com/arrow-kt/arrow/main/arrow-libs/gradle/publication.gradle"
}

allprojects {
  repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
  }
}

task generateDoc(type: Exec) {
  // Reason of commandLine: to avoid specifying the list of modules
  commandLine "sh", "gradlew", "dokka"
}

task runValidation(type: Exec) {
  commandLine "sh", "gradlew", ":docs:runAnk"
}

task buildMetaDoc {
  group = "documentation"
  description = "Generates API Doc and validates all the documentation"
  dependsOn 'generateDoc'
  dependsOn 'runValidation'
}

runValidation.mustRunAfter generateDoc