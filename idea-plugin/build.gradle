plugins {
  id "org.jetbrains.intellij" version "$INTELLIJ_GRADLE_PLUGIN_VERSION"
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.intellij'

repositories {
  maven {
    url "https://www.jetbrains.com/intellij-repository/snapshots"
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$KOTLIN_VERSION"
  implementation "org.assertj:assertj-core:$ASSERTJ_VERSION"
  api "org.celtric.kotlin:kotlin-html:$KOTLIN_HTML_VERSION"
  api project(path: ':compiler-plugin', configuration: 'shadow')

  testCompile("org.apache.ant:ant:$IDEA_APACHE_ANT_VERSION") // for the JetBrains Gradle test framework
}

compileKotlin {
  kotlinOptions.jvmTarget = "$JVM_TARGET_VERSION"
}
compileTestKotlin {
  kotlinOptions.jvmTarget = "$JVM_TARGET_VERSION"
}
jar {
  manifest {
    attributes["Specification-Title"] = project.name
    attributes["Specification-Version"] = project.version
    attributes["Implementation-Title"] = "arrow.meta.plugin.idea"
    attributes["Implementation-Version"] = project.version
  }
}

buildSearchableOptions.enabled = false

intellij {
  version = "$INTELLIJ_IDEA_VERSION"
  pluginName = "Arrow Meta Intellij IDEA Plugin"
  plugins = ["gradle", "gradle-java", "java", "org.jetbrains.kotlin:${KOTLIN_IDEA_VERSION}", "git4idea"]
}

patchPluginXml {
  /*changeNotes """
      Add change notes here.<br>
      <em>most HTML tags may be used</em>"""*/
}

runIde {
  jvmArgs '-Xmx8G'
  systemProperties['idea.is.internal'] = "false"
}

test {
  dependsOn ':publishIdeTestingDependencies'
  testLogging.showStandardStreams = true
}

apply from: 'https://raw.githubusercontent.com/arrow-kt/arrow/master/gradle/gradle-mvn-push.gradle'
